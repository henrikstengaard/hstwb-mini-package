; Startup Sequence
; ----------------
;
; Author: Henrik Noerfjand Stengaard
; Date: 2019-08-06


; add alias
Alias cls "Echo NOLINE *"*E[0;0H*E[J*" "  


; set failat to 21
FailAt 21

If Exists C:LoadModule
  C:Version exec.library version 46 >NIL:
  If Warn
    C:LoadModule ROMUPDATE
  EndIf

  ; load scsi.device, if it exists
  IF EXISTS SYS:Prefs/HstWB/Load-ScsiDevice
    IF EXISTS DEVS:scsi.device
      ; load scsi.device, if current scsi.device is less than v43.45
      Version >NIL: "scsi.device" 43 45
      IF WARN
        C:LoadModule DEVS:scsi.device
      ENDIF
    ENDIF
  ENDIF
EndIf


C:SetPatch QUIET
C:CPU CHECKINSTALL 


; Run NoClick, if use noclick prefs exists
IF EXISTS SYS:Prefs/HstWB/Use-NoClick
  NoClick
ENDIF


; Assign Startup
IF EXISTS S:Assign-Startup
  execute S:Assign-Startup
ENDIF


; Reduce buffers to mimimum
C:AddBuffers >NIL: DF0: -999
C:AddBuffers >NIL: SYS: -999


; Run add21k, if use add21k prefs exists
IF EXISTS SYS:Prefs/HstWB/Use-Add21k
  add21k
ENDIF


; Start Amiga Boot Selector
C:abs
execute S:Boot.abs


; End
LAB end
