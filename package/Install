; HstWB Mini Package Install Script
; ---------------------------------
;
; Author: Henrik Noerfjand Stengaard
; Date: 2018-05-04
;
; This script extracts and installs zip archives for HstWB Mini Package. 


FailAt 21

; Copying Workbench files
echo "Copying Workbench files..."

IF NOT EXISTS MINIDIR:C
  MakePath MINIDIR:C >NIL:
ENDIF
Copy SYSTEMDIR:C MINIDIR:C ALL >NIL:

IF NOT EXISTS MINIDIR:Devs
  MakePath MINIDIR:Devs >NIL:
ENDIF
Copy SYSTEMDIR:Devs MINIDIR:Devs ALL >NIL:

IF NOT EXISTS MINIDIR:L
  MakePath MINIDIR:L >NIL:
ENDIF
Copy SYSTEMDIR:L MINIDIR:L ALL >NIL:

IF NOT EXISTS MINIDIR:Libs
  MakePath MINIDIR:Libs >NIL:
ENDIF
Copy SYSTEMDIR:Libs MINIDIR:Libs ALL >NIL:

IF NOT EXISTS MINIDIR:Prefs
  MakePath MINIDIR:Prefs >NIL:
ENDIF
Copy SYSTEMDIR:Prefs MINIDIR:Prefs ALL >NIL:

IF NOT EXISTS MINIDIR:S
  MakePath MINIDIR:S >NIL:
ENDIF
Copy SYSTEMDIR:S MINIDIR:S ALL >NIL:


echo "Installing HstWB Mini..."
unzip -qq -o -x PACKAGEDIR:hstwb-mini.zip -d MINIDIR:


; patch fastfilesystem
; --------------------

; warning, if fastfilesystem doesn't exist
IF NOT EXISTS SYSTEMDIR:L/FastFileSystem
  echo "Warning: FastFileSystem file 'SYSTEMDIR:L/FastFileSystem'"
  echo "doesn't exist. Skip patching!"
  SKIP changeboot
ENDIF

; warning, if fastfilesystem is less than v40.1
Version >NIL: "SYSTEMDIR:L/FastFileSystem" 40 1 FILE
IF WARN
  echo "Warning: FastFileSystem file 'SYSTEMDIR:L/FastFileSystem'"
  echo "is not v40.1. Skip patching!"
  SKIP changeboot
ENDIF

; patch fastfilesystem v40.1 to v45.16, if it less than 45.16
Version "MINIDIR:L/FastFileSystem" 45 16 FILE >NIL:
IF WARN
  echo "Patching FastFileSystem to v45.16..."
  MINIDIR:C/gpatch MINIDIR:L/FastFileSystem MINIDIR:L/FastFileSystem_45.16.gpch MINIDIR:L/FastFileSystem_45.16 >NIL:
  IF $RC EQ 0 VAL
    Rename MINIDIR:L/FastFileSystem MINIDIR:L/FastFileSystem_old >NIL:
    Copy MINIDIR:L/FastFileSystem_45.16 MINIDIR:L/FastFileSystem >NIL:
  ELSE
    echo "Error: Failed patching FastFileSystem to v45.16!"
  ENDIF
ENDIF


; Change boot priority
; --------------------
LAB changeboot

; Set systemdir device boot priority to 0 and disable automount
set devicename "`execute INSTALLDIR:S/GetDeviceName "$SYSTEMDIR"`"
Assign >NIL: EXISTS $devicename
IF $RC EQ 0 VAL
  MINIDIR:C/changebootpri >NIL: $devicename 0 nomount boot
ENDIF

; Set minidir device boot priority to 1 and automount
set devicename "`execute INSTALLDIR:S/GetDeviceName "$MINIDIR"`"
Assign >NIL: EXISTS $devicename
IF $RC EQ 0 VAL
  MINIDIR:C/changebootpri >NIL: $devicename 1 mount boot
ENDIF


; End
; ---
LAB end
