; HstWB Mini Package Install Script
; ---------------------------------
;
; Author: Henrik Noerfjand Stengaard
; Date: 2019-08-06
;
; This script extracts and installs zip archives for HstWB Mini Package. 


; detect amiga os version
set amigaosversion ""
IF EXISTS "SYSTEMDIR:Libs/workbench.library"
  ; set amiga os version to 3.1.4, if workbench.library file is equal or greater than v45.194
  Version >NIL: "SYSTEMDIR:Libs/workbench.library" 45 194 FILE
  IF $RC EQ 0 VAL
    set amigaosversion "3.1.4"
  ENDIF
ELSE
  ; set amiga os version to 3.1.4, if loaded workbench.library is equal or greater than v45.194
  Version >NIL: "workbench.library" 45 194
  IF $RC EQ 0 VAL
    set amigaosversion "3.1.4"
  ENDIF
ENDIF
 

; copying amiga os files
echo "Copying Amiga OS files..."

IF NOT EXISTS MINIDIR:C
  MakePath MINIDIR:C >NIL:
ENDIF
Copy SYSTEMDIR:C MINIDIR:C ALL >NIL:

IF NOT EXISTS MINIDIR:Devs
  MakePath MINIDIR:Devs >NIL:
ENDIF
Copy SYSTEMDIR:Devs MINIDIR:Devs ALL >NIL:

IF NOT EXISTS MINIDIR:L
  MakePath MINIDIR:L >NIL:
ENDIF
Copy SYSTEMDIR:L MINIDIR:L ALL >NIL:

IF NOT EXISTS MINIDIR:Libs
  MakePath MINIDIR:Libs >NIL:
ENDIF
Copy SYSTEMDIR:Libs MINIDIR:Libs ALL >NIL:

IF NOT EXISTS MINIDIR:Prefs
  MakePath MINIDIR:Prefs >NIL:
ENDIF
Copy SYSTEMDIR:Prefs MINIDIR:Prefs ALL >NIL:

IF NOT EXISTS MINIDIR:S
  MakePath MINIDIR:S >NIL:
ENDIF
Copy SYSTEMDIR:S MINIDIR:S ALL >NIL:


echo "Installing HstWB Mini..."
unzip -qq -o -x PACKAGEDIR:hstwb-mini.zip -d MINIDIR:

; patch, if amiga os 3.1.4 is installed
IF "$amigaosversion" EQ "3.1.4"
  ; copy startup-sequence for amiga os 3.1.4
  Copy >NIL: PACKAGEDIR:Startup-Sequence.3.1.4 SYSTEMDIR:S/Startup-Sequence    

  ; install patch amiga os 3.1.4.1, if it exists
  IF EXISTS "SYSTEMDIR:S/Install-Patch-Amiga-OS-3141"
    Execute "SYSTEMDIR:S/Install-Patch-Amiga-OS-3141" SYS SYSTEMDIR QUIET
  ENDIF

  ; skip to change boot. amiga os 3.1.4 comes with an updated fastfilesystem
  SKIP changeboot
ENDIF


; patch fastfilesystem
; --------------------

; warning, if fastfilesystem doesn't exist
IF NOT EXISTS SYSTEMDIR:L/FastFileSystem
  echo "Warning: FastFileSystem file 'SYSTEMDIR:L/FastFileSystem'"
  echo "doesn't exist. Skip patching!"
  SKIP changeboot
ENDIF

; skip, if fastfilesystem is already patched to v45.16
Version "MINIDIR:L/FastFileSystem" 45 16 FILE >NIL:
IF $RC EQ 0 VAL
  echo "FastFileSystem file 'SYSTEMDIR:L/FastFileSystem' is already"
  echo "patched to v45.16. Skip patching!"
  SKIP changeboot
ENDIF

; patch fastfilesystem v44.5 to v45.16, if fastfilesystem is v44.5
; (v44.5 present on BetterWB v4.0)
Version "MINIDIR:L/FastFileSystem" 44 5 FILE >NIL:
IF $RC EQ 0 VAL
  Version "MINIDIR:L/FastFileSystem" 44 6 FILE >NIL:
  IF WARN
    echo "Patching FastFileSystem v44.5 to v45.16..."
    MINIDIR:C/gpatch MINIDIR:L/FastFileSystem MINIDIR:L/FFS_44.05_45.16.gpch MINIDIR:L/FastFileSystem_45.16 >NIL:
    IF $RC EQ 0 VAL
      Rename MINIDIR:L/FastFileSystem MINIDIR:L/FastFileSystem_44.05 >NIL:
      Copy MINIDIR:L/FastFileSystem_45.16 MINIDIR:L/FastFileSystem >NIL:
    ELSE
      echo "Error: Failed patching FastFileSystem v44.5 to v45.16!"
    ENDIF
    SKIP changeboot
  ENDIF
ENDIF

; patch fastfilesystem v40.1 to v45.16, if fastfilesystem is v40.1
; (v40.1 present on original Workbench 3.1 disks)
Version "MINIDIR:L/FastFileSystem" 40 1 FILE >NIL:
IF $RC EQ 0 VAL
  Version "MINIDIR:L/FastFileSystem" 40 2 FILE >NIL:
  IF WARN
    echo "Patching FastFileSystem v40.1 to v45.16..."
    MINIDIR:C/gpatch MINIDIR:L/FastFileSystem MINIDIR:L/FFS_40.01_45.16.gpch MINIDIR:L/FastFileSystem_45.16 >NIL:
    IF $RC EQ 0 VAL
      Rename MINIDIR:L/FastFileSystem MINIDIR:L/FastFileSystem_40.01 >NIL:
      Copy MINIDIR:L/FastFileSystem_45.16 MINIDIR:L/FastFileSystem >NIL:
    ELSE
      echo "Error: Failed patching FastFileSystem v40.1 to v45.16!"
    ENDIF
    SKIP changeboot
  ENDIF
ENDIF

; patch fastfilesystem v39.27 to v45.16, if fastfilesystem is v39.27
; (v39.27 present on ClassicWB)
Version "MINIDIR:L/FastFileSystem" 39 27 FILE >NIL:
IF $RC EQ 0 VAL
  Version "MINIDIR:L/FastFileSystem" 39 28 FILE >NIL:
  IF WARN
    echo "Patching FastFileSystem v39.27 to v45.16..."
    MINIDIR:C/gpatch MINIDIR:L/FastFileSystem MINIDIR:L/FFS_39.27_45.16.gpch MINIDIR:L/FastFileSystem_45.16 >NIL:
    IF $RC EQ 0 VAL
      Rename MINIDIR:L/FastFileSystem MINIDIR:L/FastFileSystem_39.27 >NIL:
      Copy MINIDIR:L/FastFileSystem_45.16 MINIDIR:L/FastFileSystem >NIL:
    ELSE
      echo "Error: Failed patching FastFileSystem v39.27 to v45.16!"
    ENDIF
    SKIP changeboot
  ENDIF
ENDIF


; Change boot priority
; --------------------
LAB changeboot

; set systemdir device boot priority to 0 and disable automount
set systemdirdevice "`execute INSTALLDIR:S/GetDeviceName "$SYSTEMDIR"`"
Assign >NIL: EXISTS $systemdirdevice
IF $RC EQ 0 VAL
  MINIDIR:C/changebootpri >NIL: $systemdirdevice 0 nomount boot
ENDIF

; set minidir device boot priority to 1 and automount
set minidirdevice "`execute INSTALLDIR:S/GetDeviceName "$MINIDIR"`"
Assign >NIL: EXISTS $minidirdevice
IF $RC EQ 0 VAL
  MINIDIR:C/changebootpri >NIL: $minidirdevice 1 mount boot
ENDIF

; replace systemdir device placeholder for hstwb mini amiga boot selector workbench option
rep MINIDIR:S/AmigaBootSelector/Workbench.abs "[*$SystemDirDevice]" "$systemdirdevice"

; delete systemdir load icon.library prefs, if it exists
IF EXISTS SYSTEMDIR:Prefs/HstWB/Load-IconLibrary
  Delete SYSTEMDIR:Prefs/HstWB/Load-IconLibrary >NIL:
ENDIF

; delete systemdir start abs prefs, if it exists
IF EXISTS SYSTEMDIR:Prefs/HstWB/Start-ABS
  Delete SYSTEMDIR:Prefs/HstWB/Start-ABS >NIL:
ENDIF

; End
; ---
LAB end
