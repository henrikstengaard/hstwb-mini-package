; HstWB Mini Package Install Script
; ---------------------------------
;
; Author: Henrik Noerfjand Stengaard
; Date: 2018-05-03
;
; This script extracts and installs zip archives for HstWB Mini Package. 


FailAt 21

; Fail, if FastFileSystem doesn't exist
IF EXISTS SYSTEMDIR:L/FastFileSystem
  REQUESTCHOICE "Error" "FastFileSystem file 'SYSTEMDIR:L/FastFileSystem' doesn't exist!" "OK" >NIL:
  SKIP end
ENDIF

; Fail, if FastFileSystem is less than v40.1
Version >NIL: "SYSTEMDIR:L/FastFileSystem" 40 1 FILE
IF WARN
  REQUESTCHOICE "Error" "FastFileSystem file 'SYSTEMDIR:L/FastFileSystem' is not v40.1!" "OK" >NIL:
  SKIP end
ENDIF


echo "Installing HstWB Mini..."
unzip -qq -o -x PACKAGEDIR:hstwb-mini.zip -d MINIDIR:

; Copying Workbench files
echo "Copying Workbench files..."

MakePath MINIDIR:C >NIL:
Copy SYSTEMDIR:C MINIDIR:C ALL >NIL:

MakePath MINIDIR:Devs >NIL:
Copy SYSTEMDIR:Devs MINIDIR:Devs ALL >NIL:

MakePath MINIDIR:L >NIL:
Copy SYSTEMDIR:L MINIDIR:L ALL >NIL:

MakePath MINIDIR:Libs >NIL:
Copy SYSTEMDIR:Libs MINIDIR:Libs ALL >NIL:

MakePath MINIDIR:Prefs >NIL:
Copy SYSTEMDIR:Prefs MINIDIR:Prefs ALL >NIL:

MakePath MINIDIR:S >NIL:
Copy SYSTEMDIR:S MINIDIR:S ALL >NIL:

; Patch FastFileSystem v40.1 to v45.16, if it less than 45.16
Version >NIL: "MINIDIR:L/FastFileSystem" 45 16 FILE
IF WARN
  echo "Patching FastFileSystem to v45.16..."
  MINIDIR:C/gpatch MINIDIR:L/FastFileSystem MINIDIR:L/FastFileSystem_45.16.gpch MINIDIR:L/FastFileSystem_45.16 >NIL:
  Rename MINIDIR:L/FastFileSystem MINIDIR:L/FastFileSystem_old >NIL:
  Copy MINIDIR:L/FastFileSystem_45.16 MINIDIR:L/FastFileSystem >NIL:
ENDIF

; Set systemdir device boot priority to 0 and disable automount
set devicename "`execute INSTALLDIR:S/GetDeviceName "$SYSTEMDIR"`"
Assign >NIL: EXISTS $devicename
IF $RC EQ 0 VAL
  MINIDIR:C/changebootpri >NIL: $devicename 0 nomount
ENDIF

; Set minidir device boot priority to 1 and automount
set devicename "`execute INSTALLDIR:S/GetDeviceName "$MINIDIR"`"
Assign >NIL: EXISTS $devicename
IF $RC EQ 0 VAL
  MINIDIR:C/changebootpri >NIL: $devicename 1 mount
ENDIF


; End
; ---
LAB end
