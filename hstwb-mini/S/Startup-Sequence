; Startup Sequence
; ----------------
;
; Author: Henrik Noerfjand Stengaard
; Date: 2019-08-06


; add alias
Alias cls "Echo NOLINE *"*E[0;0H*E[J*" "  

; set fail at to 21
FailAt 21

; load scsi.device, if it exists
IF EXISTS SYS:Prefs/HstWB/Load-ScsiDevice
  IF EXISTS DEVS:scsi.device
    ; load scsi.device, if current scsi.device is less than v43.45
    Version >NIL: "scsi.device" 43 45
    IF WARN
      C:LoadModule DEVS:scsi.device
    ENDIF
  ENDIF
ENDIF

; run setpatch quiet, if setpatch exists. Use noromupdate, if version is equal or greater than v44.13
IF EXISTS C:SetPatch
  Version >NIL: C:SetPatch 44 13 FILE
  IF WARN
    C:SetPatch QUIET
  ELSE
    C:SetPatch NOROMUPDATE QUIET
  ENDIF
ENDIF


; run noclick, if use noclick prefs exists
IF EXISTS SYS:Prefs/HstWB/Use-NoClick
  NoClick
ENDIF

; run assign Startup
IF EXISTS S:Assign-Startup
  execute S:Assign-Startup
ENDIF

; reduce buffers to mimimum
C:AddBuffers >NIL: DF0: -999
C:AddBuffers >NIL: SYS: -999

; run add21k, if use add21k prefs exists
IF EXISTS SYS:Prefs/HstWB/Use-Add21k
  add21k
ENDIF

; start amiga boot selector
C:abs
execute S:Boot.abs


; end
LAB end
