; Startup Sequence
; ----------------
;
; Author: Henrik Noerfjand Stengaard
; Date: 2018-01-28


; Add alias
Alias cls "Echo NOLINE *"*E[0;0H*E[J*" "  


; Set fail at to 21
FailAt 21


; Run setpatch quiet, if setpatch exists. Use noromupdate, if version is equal or greater than v44.13
IF EXISTS C:SetPatch
  Version >NIL: C:SetPatch 44 13 FILE
  IF WARN
    C:SetPatch QUIET
  ELSE
    C:SetPatch NOROMUPDATE QUIET
  ENDIF
ENDIF


; Run NoClick, if use noclick prefs exists
IF EXISTS SYS:Prefs/HstWB/Use-NoClick
  NoClick
ENDIF


; Assign Startup
IF EXISTS S:Assign-Startup
  execute S:Assign-Startup
ENDIF


; Reduce buffers to mimimum
C:AddBuffers >NIL: DF0: -999
C:AddBuffers >NIL: DH0: -999


; Run add21k, if use add21k prefs exists
IF EXISTS SYS:Prefs/HstWB/Use-Add21k
  add21k
ENDIF


; Start Amiga Boot Selector
C:abs
execute S:Boot.abs


; End
LAB end